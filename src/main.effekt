import x86/pic
import x86/pit
import x86/serial
import x86/commands
import x86/interrupts
import framebuffer
import interrupt

import ps2/keyboard

def header() = {
  val logo = """
  _____     ______  __  __     _    _    ____   _____ 
 (_____)   |  ____|/ _|/ _|   | |  | |  / __ \ / ____|
   ___     | |__  | |_| |_ ___| | _| |_| |  | | (___  
  (___)    |  __| |  _|  _/ _ \ |/ / __| |  | |\___ \ 
  _____    | |____| | | ||  __/   <| |_| |__| |____) |
 (_____)   |______|_| |_| \___|_|\_\\__|\____/|_____/ 
"""
  println(logo)
}

def dispatch { prog: => Nothing / Request }: Nothing / { Interrupt[PS2Keyboard], Interrupt[TimerTick] } = {
  try prog()
  with Request {
    def trap(n: Int) = {
      resume(println("it's a trap! ${n.show}"))
    }
    def event(n: Int) = n match {
      case 0 => resume(do interrupt(TimerTick()))
      case 1 => resume(do interrupt(PS2Keyboard()))
      case _ => resume(println("unknown event: ${n.show}"))
    }
    def special(n: Int) = n match {
      case _ => {
        println("unknown special ${n.show}")
        resume(())
      }
    }
  }
}

def os { init: => Nothing / emit[Char] }: Nothing = {
  try init()
  with emit[Char] { ch =>
    resume(print(ch.show))
  }
}

def main(): Nothing = {
  with os

  with interrupt::on[TimerTick].ignore
  with interrupt::on[PS2Keyboard].handle {keyboardHandler}
  with dispatch

  serial::init()
  header()

  pic::init()
  val inbox = interrupts::init()
  pit::init(1000)
  interrupts::idle(inbox) 
}