import x86/pic
import x86/pit
import x86/serial
import x86/commands
import x86/interrupts
import framebuffer

// import src/ps2/keyboard

def header() = {
  val logo = """
  _____     ______  __  __     _    _    ____   _____ 
 (_____)   |  ____|/ _|/ _|   | |  | |  / __ \ / ____|
   ___     | |__  | |_| |_ ___| | _| |_| |  | | (___  
  (___)    |  __| |  _|  _/ _ \ |/ / __| |  | |\___ \ 
  _____    | |____| | | ||  __/   <| |_| |__| |____) |
 (_____)   |______|_| |_| \___|_|\_\\__|\____/|_____/ 
"""
  println(logo)
}

def dispatch { prog: => Unit / Request }: Unit / { Exception[PS2Keyboard], Exception[TimerTick], Exception[RuntimeError] } = {
  try prog()
  with Request {
    def trap(n: Int) = {
      resume(println("it's a trap! ${n.show}"))
    }
    def event(n: Int) = n match {
      case 0 => resume(do raise(TimerTick(), "timer ticked"))
      case _ => resume(println("unknown event: ${n.show}"))
    }
    def special(n: Int) = n match {
      case _ => {
        resume(println("unknown special request: ${n.show}"))
      }
    }
  }
}

def ps2Handler() = ()

def main() = {
  with on[RuntimeError].panic
  with on[TimerTick].ignore
  with on[PS2Keyboard].default { ps2Handler }
  with dispatch

  serial::init()
  header()

  pic::init()
  val inbox = interrupts::init()
  pit::init(1000)
  interrupts::idle(inbox) 

  hcf() // should never run
}