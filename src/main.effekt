import x86/pic
import x86/pit
import x86/serial
import x86/commands
import x86/interrupts
import framebuffer
import interrupt

import ps2/keyboard
import calculator
import debug
import utils

import scanner

def header(): Unit / Debug = {
  val logo = """
  _____     ______  __  __     _    _    ____   _____ 
 (_____)   |  ____|/ _|/ _|   | |  | |  / __ \ / ____|
   ___     | |__  | |_| |_ ___| | _| |_| |  | | (___  
  (___)    |  __| |  _|  _/ _ \ |/ / __| |  | |\___ \ 
  _____    | |____| | | ||  __/   <| |_| |__| |____) |
 (_____)   |______|_| |_| \___|_|\_\\__|\____/|_____/ 
"""
  do println(logo)
}

def fault(fault: Int) = {
  panic("""
=============
=== FAULT ===
=============

${fault.show}
""")
}

def os { init: => Nothing / emit[Char] }: Nothing = {
  with looping
  with source[Char] { init }
  with emit[Char] { ch => framebuffer::print(ch.show) }

  calculator::program()
}

def main(): Nothing = {
  with debug

  serial::init()
  header()

  val inbox = interrupts::init(box fault)
  pic::init()
  pit::init(1000)

  with os
  with interrupt::on[TimerTick].ignore
  with interrupt::on[PS2Keyboard].handle { keyboard::handler }
  with interrupt::on[Cascade].ignore
  with interrupt::on[COM2].ignore
  with interrupt::on[COM1].ignore
  with interrupt::on[LPT2].ignore
  with interrupt::on[FloppyDisk].ignore
  with interrupt::on[LPT1].ignore
  with interrupt::on[CMOS].ignore
  with interrupt::on[Peripheral1].ignore
  with interrupt::on[Peripheral2].ignore
  with interrupt::on[Peripheral3].ignore
  with interrupt::on[PS2Mouse].ignore
  with interrupt::on[FPU].ignore
  with interrupt::on[PrimaryATA].ignore
  with interrupt::on[SecondaryATA].ignore
  with dispatch
  interrupts::idle(inbox) 
}